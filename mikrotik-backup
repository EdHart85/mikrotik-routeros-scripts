# This configuration was develop with the help of Mikrotik's documentation and several others sources that I found on the Internet.
# It should work for most simple enviroments or at least help you build your own configuration for different scenarios.
# Tested only on RouterOS version 7.x.

# by Eduardo Hartmann (https://github.com/EdHart85)


# EDIT HERE
:local cloudpwd ("AVeryVeryStrongKey")
:local cloudbkpname ("my-router-backup")
# EDIT END

:log info ("=============BACKUP STARTED=============" );

# Months array
:local months ("jan","feb","mar","apr","may","jun","jul","aug","sep","oct","nov","dec");

# Make sure your data/time is always up to date
# Get time
:local ts [/system clock get time]
:set ts ([:pick $ts 0 2].[:pick $ts 3 5].[:pick $ts 6 8])
# Get Date
:local ds [/system clock get date]

# Convert name of month to number
:local month [ :pick $ds 0 3 ];
:local mm ([ :find $months $month -1 ] + 1);
:if ($mm < 10) do={ :set mm ("0" . $mm); }

# Set $ds to YYYY-MM-DD format
:set ds ([:pick $ds 7 11] . $mm . [:pick $ds 4 6])

# File name for system backup
:local bkpname1 ([/system identity get name]."-".$ds."-".$ts.".backup")

# File name for config export
:local bkpname2 ([/system identity get name]."-".$ds."-".$ts.".rsc")

# Backup the data
/system backup save name=$bkpname1
:log info message="System backup finished (1/3).";

/export compact file=$bkpname2
:log info message="Config export finished (2/3)."

/system backup cloud upload-file name=$cloudbkpname action=create-and-upload password=$cloudpwd replace=$cloudbkpname
:log info message="Cloud backup finished (3/3)."

:log info ("=============BACKUP FINISHED=============" );
